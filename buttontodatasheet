#include <Bridge.h>
#include <Process.h>

const int buttonPin = 7;
bool buttonState = false;
bool alertState = false;
bool lastButton = HIGH;
bool debounce = false;
unsigned long debounceTime = 50;
unsigned long lastDebounceTime = 0;

String deviceID = "v3A3A048C2D3951C";
String pushingBoxURL = "http://api.pushingbox.com/pushingbox";

void setup() {
  pinMode(buttonPin, INPUT_PULLUP);
  Bridge.begin();
  Serial.begin(9600);
  while (!Serial);
  Serial.println("Setup complete.");
}

void loop() {
  bool reading = digitalRead(buttonPin);

  if (reading != lastButton) {
    lastDebounceTime = millis();
    debounce = true;
  }

  if (debounce && (millis() - lastDebounceTime) > debounceTime) {
    if (reading == LOW) {
      buttonState = !buttonState;
      alertState = !alertState;

      String url = pushingBoxURL + "?devid=" + deviceID +
                   "&ButtonState=" + (buttonState ? "On" : "Off") +
                   "&AlertState=" + (alertState ? "High" : "Low");
      
      Serial.println("Sending to PushingBox:");
      Serial.println(url);

      sendToPushingBox(url);
    }
    debounce = false;
  }

  lastButton = reading;
}

void sendToPushingBox(String url) {
  Process p;
  p.begin("curl");
  p.addParameter(url);
  p.run();

  while (p.available()) {
    char c = p.read();
    Serial.print(c);
  }
  Serial.println();
}

